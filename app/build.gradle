/**
 * The first line in the build configuration applies the Android Gradle plugin
 * to this build and makes the android block available to specify Android-specific build options.
 */
plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-kapt'
}
/**
 * The android block is where you configure all your Android-specific build options.
 */
android {
    def appId = 'com.humara.nagar'
    //The app's namespace. Used primarily to access app resources.
    namespace appId
    compileSdk rootProject.compileSdkVersion
    /**
     * The defaultConfig block encapsulates default settings and entries for all build variants and can override some attributes in main/AndroidManifest.xml
     * dynamically from the build system. You can configure product flavors to override these values for different versions of your app.
     */
    defaultConfig {
        // Uniquely identifies the package for publishing.
        applicationId appId
        minSdk rootProject.minSDKVersion
        targetSdk rootProject.targetSDKVersion
        versionCode 1
        versionName "1.0.0"
        archivesBaseName = "app-" + versionName + "-" + versionCode
        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        vectorDrawables.useSupportLibrary = true
        resConfigs 'en', 'hi'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            crunchPngs true
            //TODO: Add signing configs before release
        }
        debug {
            minifyEnabled false
            shrinkResources false
            crunchPngs false
            buildConfigField "String", "BASE_URL", "\"https://humara-nagar-server-389413.el.r.appspot.com\""
        }
    }
    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        sourceCompatibility sourceCompatibilityVersion
        targetCompatibility targetCompatibilityVersion
    }
    kotlin {
        jvmToolchain(11)
    }
    kotlinOptions {
        freeCompilerArgs += [
                '-Xjvm-default=enable'
        ]
    }
    buildFeatures {
        dataBinding true
    }
}

dependencies {
    // Dependency on local binaries
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.core:core-ktx:1.10.0'
    //splash screen
    implementation 'androidx.core:core-splashscreen:1.0.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation "androidx.fragment:fragment-ktx:1.5.7"
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$rootProject.lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$rootProject.lifecycleVersion"
    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"
    implementation 'androidx.annotation:annotation:1.6.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.10.0'
    //Glide
    implementation 'com.github.bumptech.glide:glide:4.14.2'
    //Room Database
    implementation "androidx.room:room-ktx:$rootProject.roomVersion"
    kapt "androidx.room:room-compiler:$rootProject.roomVersion"
    // OTP pin view
    implementation 'io.github.chaosleung:pinview:1.4.4'
    //security-crypto for encrypted shared preference
    implementation "androidx.security:security-crypto:1.0.0"
    //firebase
    implementation platform('com.google.firebase:firebase-bom:31.4.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-messaging'
    //Google play-services
    implementation 'com.google.android.gms:play-services-location:21.0.1'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'
    //Expandable TextView
    implementation 'at.blogc:expandabletextview:1.0.5'
    //swipe refresh layout
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01"
    //shimmer
    implementation 'com.facebook.shimmer:shimmer:0.5.0'
    def kohii_version = "1.4.0.2017001"
    implementation "im.ene.kohii:kohii-core:${kohii_version}" // core library
    implementation "im.ene.kohii:kohii-exoplayer:${kohii_version}" // default support for ExoPlayer
    //Do NOT update this library as kohii library is dependent on it
    implementation "com.google.android.exoplayer:exoplayer:2.17.1"
    //image picker
    implementation 'com.github.esafirm:android-image-picker:3.0.0'
    // zoom image view
    implementation 'com.github.MikeOrtiz:TouchImageView:3.3'
    //ToolTip -> Balloon
    implementation "com.github.skydoves:balloon:1.5.2"
    //test dependencies
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
    androidTestImplementation "androidx.room:room-testing:$rootProject.roomVersion"
}